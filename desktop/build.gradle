plugins {
    id 'scala'
}

sourceCompatibility = JavaVersion.VERSION_21
targetCompatibility = JavaVersion.VERSION_21

compileScala {
    scalaCompileOptions.deprecation = true
    scalaCompileOptions.unchecked = true
    scalaCompileOptions.optimize = false
    scalaCompileOptions.additionalParameters = [
            '-feature',
            '-language:higherKinds',
            '-language:existentials',
            '-encoding', 'UTF-8',
            '-Ybackend-parallelism', '4',
            '-Xfatal-warnings'
    ]
}

dependencies {
    implementation 'org.scala-lang:scala3-library_3:3.6.3'
    implementation "com.github.crykn.guacamole:gdx-desktop:$guacamoleVersion"
}

sourceSets.main.scala.srcDirs = ["src/"]
sourceSets.main.resources.srcDirs = ["../assets"]

project.ext.mainClassName = "org.merlin.tertis.DesktopLauncher"
project.ext.assetsDir = new File("../assets")

import org.gradle.internal.os.OperatingSystem

task run(dependsOn: classes, type: JavaExec) {
    mainClass = project.mainClassName
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
    workingDir = project.assetsDir
    ignoreExitValue = true

    if (OperatingSystem.current() == OperatingSystem.MAC_OS) {
        // Required to run on macOS.. No longer necessary with Guacamole but saves a fork.
        jvmArgs += "-XstartOnFirstThread"
    }
}

task debug(dependsOn: classes, type: JavaExec) {
    mainClass = project.mainClassName
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
    workingDir = project.assetsDir
    ignoreExitValue = true
    debug = true
}

task dist(type: Jar) {
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
    manifest {
        attributes 'Main-Class': project.mainClassName
    }
    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
}


dist.dependsOn classes

eclipse.project.name = appName + "-desktop"
